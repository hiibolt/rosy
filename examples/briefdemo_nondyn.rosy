{******************************************************************************
*                                                                             *
*                            briefdemo_nondyn.rosy                            *
*                                                                             *
*                          COSY DATA TYPE BRIEF DEMO                          *
*                                                                             *
*     Non-dynamic version: polymorphic variables X1/X2/X3 are split into      *
*     type-specific variants per procedure to satisfy Rosy's static typing.   *
*                                                                             *
******************************************************************************}
BEGIN ;
VARIABLE NO 1 ; VARIABLE NV 1 ; VARIABLE LX 1 ;

PROCEDURE RUN ;
   VARIABLE NM 1 ;
   VARIABLE A 1 ; VARIABLE B 1 ;

   PROCEDURE COSYST ; WRITE 6 ' ' '** COSY STrings **' ' ' ;
      VARIABLE (ST) X1 1 ; VARIABLE (ST) X2 1 ;
      X1 := 'Hello World!' ;
      {Extraction and concatenation}
      X2 := X1|(7&8) ; X2 := X2&(X1|7)&(X1|LENGTH(X1)) ;
      WRITE 6 X2&' '&X1 ;
      WRITE 6 ' ' ;
      WRITE 6 'The above string was crafted by manipulating ''Hello World!''.' ;
      ENDPROCEDURE ;

   PROCEDURE COSYLO ; WRITE 6 ' ' '** COSY LOgicals **' ' ' ;
      VARIABLE (LO) X1 1 ; VARIABLE (LO) X2 1 ;
      X1 := LO(1) ;
      X2 := LO(0) ;
      WRITE 6 '('&ST(X1)&')*('&ST(X2)&') is '&ST(X1*X2) ;
      WRITE 6 '('&ST(X1)&')+('&ST(X2)&') is '&ST(X1+X2) ;
      ENDPROCEDURE ;

   PROCEDURE COSYCM ; WRITE 6 ' ' '** COSY CoMplex numbers **' ' ' ;
      VARIABLE (CM) IM 1 ; VARIABLE (CM) X1 1 ;
      IM := CM(0&1) ;
      X1 := CM(A&B) ;
      WRITE 6 'IM := CM(0&1) is '&ST(IM)&'  : imaginary unit' ;
      WRITE 6 'X1 := CM(A&B) is '&ST(X1) ;
      WRITE 6 ' ' ;
      WRITE 6 'Extracting the real      part of X1: '&ST(X1|1) ;
      WRITE 6 'Extracting the imaginary part of X1: '&ST(X1|2) ;
      ENDPROCEDURE ;

   PROCEDURE COSYVE ; WRITE 6 ' ' '** COSY VEctors **' ' ' ;
      VARIABLE (VE) X1 1 ; VARIABLE (VE) X2 1 ;
      X1 := A&B ;
      X2 := X1&X1 ;
      WRITE 6 'X1 := A&B is' X1 ;
      WRITE 6 'X2 := X1&X1 is' X2 ;
      WRITE 6 ' ' ;
      WRITE 6 'Extracting the component  3      from X2:' X2|3 ;
      WRITE 6 'Extracting the components 2 to 4 from X2:' X2|(2&4) ;
      ENDPROCEDURE ;

   PROCEDURE COSYDA ; WRITE 6 ' ' '** COSY DA''s **' ' ' ;
      VARIABLE (DA) X1 1 ; VARIABLE (DA) X2 1 ; VARIABLE (DA) X3 1 ;
      WRITE 6 'DA(1), the 1-st identity, is' DA(1) ;
      X1 := A+DA(1) ; X2 := B+DA(2) ; X3 := X1-2*SQR(X2) ;
      WRITE 6 'X3 := X1-2*SQR(X2) where X1=A+DA(1), X2=B+DA(2)' X3 ;
      WRITE 6 ' ' ;
      WRITE 6 'Extracting the (0,1) coefficient from X3: '&ST(X3|(0&1)) ;
      WRITE 6 ' ' ;
      X1 := X3%(-1) ;
      WRITE 6 'Integral   of X3 w.r.t. the 1st variable' X1 ;
      X2 := X1%1 ;
      WRITE 6 'Derivative of it w.r.t. the 1st variable' X2 ;
      ENDPROCEDURE ;

   PROCEDURE COSYCD ; WRITE 6 ' ' '** COSY Complex DA''s **' ' ' ;
      WRITE 6 'CD(1), the 1-st identity, is' CD(1) ;
      ENDPROCEDURE ;

   A := 2 ; B := 3 ; WRITE 6 ' ' 'A='&ST(A)&'  B='&ST(B) ' ' ;

   COSYST ; COSYLO ; COSYCM ; COSYVE ;
   DAINI NO NV 0 NM ; COSYDA ; COSYCD ;

   ENDPROCEDURE ;

NO := 3 ; NV := 2 ;
LX := VMAX( LST(80) & LCD(NO&NV) ) ;
WRITE 6 'The estimated variable size LX is '&(ST(LX)|(1&8)) ;

RUN ; END ;
