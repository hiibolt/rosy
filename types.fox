BEGIN;

PROCEDURE RUN;
VARIABLE I 1;
VARIABLE N 1;
    PROCEDURE EXAMPLE1;     {Vectors vs Arrays}
    VARIABLE X 1 N;     {A 1D array with N entries, each entry is a real number (assigned a memory of 1)}
    VARIABLE Y N;     {A vector of length N}

    {One main difference is that arrays has a fixed size once they are declared, so its entries will have a value (typically 0) even if you didn't declare it. Vectors have a dynamic size up to the declared size. See the following example.}  

    {Assigning a value for some elements}
    X(1):=1;
    Y:=2;
    LOOP I 1+1 N/2; 
        X(I):=1;
        Y:=Y&2;
    ENDLOOP;

    {This following should print all X elements with a value of 1 for the ones we have assigned and zero for the ones we have not}
    LOOP I 1 N;
        WRITE 6 'For I = '&ST(I)&', X(I) = '&ST(X(I));
    ENDLOOP;

    {You should get an error message here after the 5th element. COSY here considers the vector having only a length of 5 although we have declared it to 10.}
    LOOP I 1 N;
        {WRITE 6 'For I = '&ST(I)&', Y|I = '&ST(Y|I);}
    ENDLOOP;  {If you want to add more elements later, comment out this loop first}

    LOOP I (N/2)+1 N; 
        X(I):=1;
        Y:=Y&2;
    ENDLOOP;

    LOOP I 1 N;
        WRITE 6 'For I = '&ST(I)&', X(I) = '&ST(X(I))&', Y|I = '&ST(Y|I);
    ENDLOOP;

    ENDPROCEDURE;   {EXAMPLE1}

    PROCEDURE EXAMPLE2;     {Nested Types}
    VARIABLE X N 3;     {A 1D array with 3 entries, each entry is a vector of length N (Actually, the memory assigned for each entry is N, so we can have N elements each has a memory of 1, real number).}

    X(1):=1;
    X(2):=2;
    X(3):=3;
    LOOP I 1 N-1; 
        X(1):=X(1)&1;
        X(2):=X(2)&2;
        X(3):=X(3)&3;
    ENDLOOP;

    {Use parenthesis to access array index}
    WRITE 6 'X(1) = ';
    WRITE 6 X(1);
    WRITE 6 'X(2) = ';
    WRITE 6 X(2);
    WRITE 6 'X(3) = ';
    WRITE 6 X(3);

    {Use vertical bar "|" to access vector elements of an array}
    WRITE 6 'The 4th and 5th elements of X(1) are: '&ST(X(1)|4)&' and '&ST(X(1)|5);

    ENDPROCEDURE;   {EXAMPLE2}

    PROCEDURE EXAMPLE3;     {4D Array}
    VARIABLE X 1 2 3 4 N;   {The first number is always for memory (1 since each element is a real number). The next number is the first dimension, and the one after is the second dimension, and so on.}
    VARIABLE J 1;
    VARIABLE K 1;
    VARIABLE L 1;

    LOOP L 1 2;
        LOOP K 1 3;
            LOOP J 1 4;
                LOOP I 1 N;
                    X(L,K,J,I):=((L-1)*(K-1)*(J-1))*I;
                ENDLOOP;
            ENDLOOP;
        ENDLOOP;
    ENDLOOP;

    WRITE 6 X(2,3,4,10);    {This is the last element, but you can access any element this way}

    ENDPROCEDURE;   {EXAMPLE3}

N:=10;

{EXAMPLE1;}

EXAMPLE2;

{EXAMPLE3;}

ENDPROCEDURE;

RUN;   {RUN}
END;
